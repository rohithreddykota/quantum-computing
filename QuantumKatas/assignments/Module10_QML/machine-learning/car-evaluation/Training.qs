// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.

namespace Microsoft.Quantum.Samples {
    open Microsoft.Quantum.Convert;
    open Microsoft.Quantum.Intrinsic;
    open Microsoft.Quantum.Canon;
    open Microsoft.Quantum.Arrays;
    open Microsoft.Quantum.MachineLearning;
    open Microsoft.Quantum.Math;

    function WithProductKernel(scale : Double, sample : Double[]) : Double[] {
        return sample + [scale * Fold(TimesD, 1.0, sample)];
    }

    function Preprocessed(samples : Double[][]) : Double[][] {
        let scale = 1.0;

        return Mapped(
            WithProductKernel(scale, _),
            samples
        );
    }

    function DefaultSchedule(samples : Double[][]) : SamplingSchedule {
        return SamplingSchedule([
            0..Length(samples) - 1
        ]);
    }

    function ClassifierStructure() : ControlledRotation[] {
        return [
            ControlledRotation((0, []), PauliZ, 1),
            ControlledRotation((1, []), PauliX, 0),
            ControlledRotation((0, [1]), PauliZ, 3),
            // ControlledRotation((2, []), PauliX, 2),
            // ControlledRotation((2, [1]), PauliZ, 5),
            // ControlledRotation((2, [0]), PauliZ, 4),
        ];
    }

    operation TrainHalfMoonModel(
        trainingVectors : Double[][],
        trainingLabels : Int[],
        initialParameters : Double[][]
    ) : (Double[], Double) {
        let samples = Mapped(
            LabeledSample,
            Zipped(Preprocessed(trainingVectors), trainingLabels)
        );
        Message("Ready to train.");
        let classifierStructure = ClassifierStructure();
        // message length of classifierStructure is 8
        Message($"Classifier structure length: {Length(classifierStructure)}");
        let mapped = Mapped(
                SequentialModel(ClassifierStructure(), _, 0.0),
                initialParameters
            );
        // message length of mapped is 4
        Message($"Mapped length: {Length(mapped)}");
        // print the first element of mapped
        Message($"Mapped[0]: {mapped[0]}");
        // message lenght samples is 100
        Message($"Samples length: {Length(samples)}");
        Message($"Mapped length: {Length(mapped)}");
        let (optimizedModel, nMisses) = TrainSequentialClassifier(
            mapped,
            samples,
            DefaultTrainingOptions()
                w/ LearningRate <- 0.3
                w/ MinibatchSize <- 1000
                w/ Tolerance <- 0.005
                w/ NMeasurements <- 25
                w/ MaxEpochs <- 4
                w/ VerboseMessage <- Message,
            DefaultSchedule(trainingVectors),
            DefaultSchedule(trainingVectors)
        );
        Message($"Training complete, found optimal parameters: {optimizedModel::Parameters}");
        return (optimizedModel::Parameters, optimizedModel::Bias);
    }

    operation ValidateHalfMoonModel(
        validationVectors : Double[][],
        validationLabels : Int[],
        parameters : Double[],
        bias : Double
    ) : Double {
        let samples = Mapped(
            LabeledSample,
            Zipped(Preprocessed(validationVectors), validationLabels)
        );
        let tolerance = 0.005;
        let nMeasurements = 10000;
        let results = ValidateSequentialClassifier(
            SequentialModel(ClassifierStructure(), parameters, bias),
            samples,
            tolerance,
            nMeasurements,
            DefaultSchedule(validationVectors)
        );
        return IntAsDouble(results::NMisclassifications) / IntAsDouble(Length(samples));
    }

    operation ClassifyHalfMoonModel(
        samples : Double[][],
        parameters : Double[],
        bias : Double,
        tolerance  : Double,
        nMeasurements : Int
    )
    : Int[] {
        let model = Default<SequentialModel>()
            w/ Structure <- ClassifierStructure()
            w/ Parameters <- parameters
            w/ Bias <- bias;
        let features = Preprocessed(samples);
        let probabilities = EstimateClassificationProbabilities(
            tolerance, model,
            features, nMeasurements
        );
        return InferredLabels(model::Bias, probabilities);
    }

}
